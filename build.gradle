plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'org.ajoberstar.grgit' version '4.1.0'
}

sourceCompatibility = "17"
targetCompatibility = "17"

archivesBaseName = "${project.archives_base_name}-for-${project.minecraft_version}-build-20"
version = "${project.mod_version}+${project.minecraft_version}-build-20"
group = project.maven_group

loom {
    mixin.defaultRefmapName = "mixins.optimium.refmap.json"
    accessWidenerPath = file("src/main/resources/optimium.accesswidener")
}

configurations {
    modIncludeImplementation

    include.extendsFrom modIncludeImplementation
    modImplementation.extendsFrom modIncludeImplementation
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    implementation 'org.joml:joml:1.10.4'
    include 'org.joml:joml:1.10.4'

    // Fabric API
    modIncludeImplementation(fabricApi.module("fabric-api-base", project.fabric_version))
    modIncludeImplementation(fabricApi.module("fabric-rendering-fluids-v1", project.fabric_version))
    modIncludeImplementation(fabricApi.module("fabric-rendering-data-attachment-v1", project.fabric_version))
    modIncludeImplementation(fabricApi.module("fabric-resource-loader-v0", project.fabric_version))
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}